alias vendingMachine d0
alias dial d1
alias button d2
alias stacker d3
alias sorter d4

alias requestHash r8
alias amountAvailable r9
alias requestAmount r10
alias partialRequest r11

define sorterOut 0
define sorterReturn 1


move sp 0
push 1724793494  # Coal
push 1758427767  # Iron
push -707307845  # Copper
push 1103972403  # Silicon
push -190236170  # Lead
push 1830218956  # Nickel
push -916518678  # Silver
push -1348105509 # Gold
push -983091249  # Cobalt
push -1516581844 # Uranium
push -1805394113 # Ice Oxite
push 1253102035  # Ice Volatiles
push 1217489948  # Ice Water
push -1641500434 # Reagent Mix


reset:
s sorter Mode 2

s dial Mode 14 # max value = number of item types (setting 0 is reserved for starting defragmentation)
yield
s dial Setting 1

idle: # TODO: actual idle so it doesn't have to re-count constantly
yield
l r0 dial Setting
beqz r0 defrag
move sp r0 # sp=1 will peek index 0
peek requestHash
s db Setting requestHash # for hash display to read

l r0 button Setting
beqz r0 idle

move r0 1 # first storage slot in vending machine - 1
move amountAvailable 0 # total
countItems:
add r0 r0 1
bgt r0 101 finishCounting
ls r1 vendingMachine r0 OccupantHash
bne r1 requestHash countItems
ls r1 vendingMachine r0 Quantity
add amountAvailable amountAvailable r1
j countItems
finishCounting:

min r0 amountAvailable 50
s dial Mode amountAvailable
yield
s dial Setting r0
waitForAmount:
yield
l requestAmount dial Setting
l r0 button Setting
beqz r0 waitForAmount
beqz requestAmount reset

s stacker Mode 1
outputLoop:
yield
min partialRequest requestAmount 50
s stacker Setting partialRequest
fillStacker:
ls r1 stacker 2 Quantity
bge r1 partialRequest skipFillStacker
s vendingMachine RequestHash requestHash
awaitItemToStacker:
yield
ls r2 stacker 2 Quantity
ble r2 r1 awaitItemToStacker
blt r2 partialRequest fillStacker
skipFillStacker:
s stacker Output 1
jal _awaitItemToSorter
s sorter Output sorterOut
sub requestAmount requestAmount partialRequest
bgtz requestAmount outputLoop

ls r0 stacker 2 Quantity
beqz r0 reset
s stacker Setting 50
s stacker Activate 1
jal _awaitItemToSorter
s sorter Output sorterReturn
j reset




defrag:


_awaitItemToSorter:
yield
ls r0 sorter 0 Occupied
beqz r0 _awaitItemToSorter
yield
j ra


DEBUG:
s db Setting 1337
