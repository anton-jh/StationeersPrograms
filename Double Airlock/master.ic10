alias doorIntA d0
alias doorIntB d1
alias doorExtA d2
alias doorExtB d3
alias override d4
alias slaveA d5

alias mode r12

define deprPressure 0.5

define cmdIdle 100
define cmdDepr 110 # use this + mode
define cmdPres 120 # use this + mode


init:
s doorIntA Setting 0
s doorIntB Setting 0
s doorExtA Setting 0
s doorExtB Setting 0
s doorIntA Mode 1
s doorIntB Mode 1
s doorExtA Mode 1
s doorExtB Mode 1

jal _checkOverride
jal _closeAllDoors
sleep 1
move mode 0
jal _setDoors


idle:
jal _checkOverride
yield

l r0 doorIntA Open
l r1 doorIntA Setting
sub r0 r1 r0
bgtz r0 cycleAirlock
l r0 doorIntB Open
l r1 doorIntB Setting
sub r0 r1 r0
bgtz r0 cycleAirlock
l r0 doorExtA Open
l r1 doorExtA Setting
sub r0 r1 r0
bgtz r0 cycleAirlock
l r0 doorExtB Open
l r1 doorExtB Setting
sub r0 r1 r0
bgtz r0 cycleAirlock

j idle

cycleAirlock:
seqz mode mode
s db Setting mode

jal _closeAllDoors
add r0 cmdDepr mode
s slaveA Setting r0
yield

waitForDepr:
jal _checkOverride
yield
l r0 db Setting
beqz r0 waitForDepr

add r0 cmdPres mode
s slaveA Setting r0
yield

waitForPres:
jal _checkOverride
yield
l r0 db Setting
beqz r0 waitForPres

s slaveA Setting cmdIdle
jal _setDoors
j idle


_setDoors:
seqz r0 mode
s doorIntA Open r0
s doorIntB Open mode
s doorExtA Open mode
s doorExtB Open r0
j ra


_checkOverride:
l r0 override Setting
beqz r0 ra

s slaveA Setting cmdIdle
jal _closeAllDoors
s doorIntA Mode 0
s doorIntB Mode 0
s doorExtA Mode 0
s doorExtB Mode 0

overrideLoop:
yield
l r0 override Setting
beqz r0 init
j overrideLoop


_closeAllDoors:
s doorIntA Open 0
s doorIntB Open 0
s doorExtA Open 0
s doorExtB Open 0
j ra
